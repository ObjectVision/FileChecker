container FileChecker
{
	// To Configure: path needs to be without spaces and replace backslashed with double forward slashes!
	// parameter<string> FOI     := '"C:/LocalData/rvnl_materialen/Export/Materialen/20231113_WLO_Hoog_BAU';       //Folder Of Interest
	parameter<string> FOI     := '"C:/LocalData/rvnl_materialen/Export_20231219';       //Folder Of Interest  
	// parameter<string> FOI     := '"C:/LocalData/rvnl_materialen/20230704_ExportNaarCMLLeiden_20230704/NaarCMLLeiden';       //Folder Of Interest
	// parameter<string> FOI     := '"C:/LocalData/rvnl_materialen/Export_20231106';       //Folder Of Interest
	// parameter<string> FOI     := Expand(., '"%ProjDir%/data');
	//
	
	parameter<bool> WriteFilesCSV := GetFolderInfo/writeObjectList == 0 ? TRUE : FALSE;
	
	container GetFolderInfo
	{
		parameter<string> FOI_edit          := replace(FOI, '/', '\\');
		parameter<string> ComSpec           := Expand(., '%env:ComSpec%');
		parameter<string> FileNameDirInfo   := Expand(., '%LocalDataProjDir%/dirinfo.csv');
		parameter<string> DirCmdOrgMD       := Expand(., 'MD "%LocalDataProjDir%"');
		parameter<string> DirCmdOrgHeader   := 'echo path > '+FileNameDirInfo;
		parameter<string> DirCmdOrgDir      := 'Dir '+ FOI_edit +'\\*.tif" >> ' + FileNameDirInfo ;

		parameter<string> DirCmd            := Replace(DirCmdOrgMD, '/', '\\') + ' & ' + Replace(DirCmdOrgHeader, '/', '\\') + ' & ' + Replace(DirCmdOrgDir, '/', '\\') + ' /B /S';
		parameter<string> NewLine           : ['\n'];
		parameter<uint32> writeObjectList   := exec_ec(ComSpec, '/c ' + DirCmd, Expand(., '%LocalDataProjDir%'));
	}
	
	unit<uint32> Files 
	: Storagename = "=GetFolderInfo/FileNameDirInfo"
	, StorageType = "gdal.vect"
	, StorageReadOnly = "true"
	{
		attribute<string> name         := AsItemName(path);
		attribute<string> name_cleaned := replace(name, AsItemName(replace(FOI, '"','')), '');
	}
	
	container Read_Files :=
		for_each_ne(
			Files/name
			, 'Read_Files_T('+quote(Files/path)+')'
		), ExplicitSuppliers = "WriteFilesCSV";
		
	Template Read_Files_T
	{
		parameter<string> path;
		//
		
		unit<ipoint> Read_unit
		: StorageName = "=path"
		, StorageType = "gdal.grid"
		, StorageReadOnly = "true"
		{
			attribute<uint64>  GridData;  //value type needs to be derived from tif-file.
			parameter<bool>    All_Is_Zero := All(IsZero(GridData));
			parameter<bool>    All_Is_Null := All(IsNull(GridData));
			parameter<uint64>  Sum         := sum_uint64(GridData);
		}
	}
	
	unit<uint32> Results := Files
	{
		attribute<string> name         := Files/name;
		attribute<string> name_cleaned := Files/name_cleaned;
		
		attribute<uint64> Sum          := ='union_data(., '+AsItemList('Read_Files/'+name+'/Read_unit/Sum')+')';
		attribute<bool>   Is_all_zero  := ='union_data(., '+AsItemList('Read_Files/'+name+'/Read_unit/All_Is_Zero')+')';
		attribute<bool>   Is_all_null  := ='union_data(., '+AsItemList('Read_Files/'+name+'/Read_unit/All_Is_Null')+')';
	}
}
